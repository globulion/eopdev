#!/usr/bin/gnuplot
#
# ------------------------------------------------
# Figure 1. Charge-Transfer Energy OEP Paper.
# Bartosz BÅ‚asiak, Gundelfingen 12 November 2019
# ------------------------------------------------
#
set terminal epslatex size 7.1cm,12cm color colortext standalone \
   font ", 8" \
   header \
   "\\newcommand{\\ft}[0]{\\footnotesize}\\usepackage{amsmath}\\usepackage{amssymb}\\usepackage{upgreek}\\usepackage{tikz}\\usetikzlibrary{arrows.meta,calc,decorations.markings,math,arrows.meta,shapes.arrows}"

c_border = "#515151"
c_side = "#8D8D8D" # c7c7c7
c_ol   = "#0c8a1b"
c_est  = "#333333"
c_efp2 = "#0035ba"

set style line 1 lc rgb c_ol   lt 1 lw 2 pt 7 pi -1 ps 1.0   # OEP
set style line 2 lc rgb c_ol   lt 1 lw 2 pt 6 pi -1 ps 1.0   # OL
set style line 3 lc rgb c_efp2 lt 1 lw 2 pt 7 pi -1 ps 1.0   # EFP2
set style line 4 lc rgb c_est  lt 1 lw 2 pt 7 pi -1 ps 1.5   # Est
set style line 5 lc rgb c_side lt 1 lw 2 pt 7 pi -1 ps 1.0   # Eint(HF)

set style line 6 lc rgb c_border lt 1 lw 1 

set pointintervalbox 1.2

# global settings for each subplot
#set border 1+2+4 lw 3 lc rgbcolor c_border
#set border lw 3 lc "red"

unset key
set grid x y

# output file
set output 'fig-1.tex'
set loadpath "./data/"
set multiplot layout 3,1 #margins 0.1,0.98,0.1,0.98 #spacing 0.08,0.08

# physical scaling factors
c1 = 627.509             # --> kcal/mole


# origins
ox   = 0.220
o11x = ox; o11y = 0.60;
o21x = ox; o21y = 0.28;
o31x = ox; o31y =-0.04;

# R2 box position
ob1x = 0.07; ob1y = 0.86;
ob2x = 0.59; ob2y = 0.96;

# scale
s    = 0.5;

# margins
set bmargin 0
set lmargin 0
set rmargin 0
set tmargin 0

# label positions
graph_label_x = 0.07
graph_label_y = 0.88

# ------------------------------------------------------------------------------------------------- #
#set object 388 rectangle from screen 0.01, screen 0.92 to screen 0.99, screen 0.913 fc rgb "#ffdeff" fillstyle solid 0.7 noborder
#set object 389 rectangle from screen 0.01, screen 0.00 to screen 0.99, screen 0.007 fc rgb "#ffdeff" fillstyle solid 0.7 noborder
#set label 488 '\LARGE\shortstack{Ab Initio DMS\\$R_{\text{max}}=1$}' front at screen 0.189, screen 0.96
#set label 489 '\LARGE\shortstack{Generalized DMS\\$R_{\text{max}}=2$}' front at screen 0.47, screen 0.96
#set label 490 '\LARGE\shortstack{Distributed\\Polarizability}' front at screen 0.762, screen 0.96
#set arrow 1 from 1.0, -3 to 1.0, 0.05  nohead lw 3 lc rgbcolor c_side
# ------------------------------------------------------------------------------------------------- #

# ------ Graph I
set format x "%2.1f"
set format y "%2.1f"
set format y2 "%3.1f"

set xrange [-0.3:1.0]
set yrange [-3:0.05]
set y2range [-4.6:-2.2]

set size ratio 0.8 s, s
set origin o11x, o11y
set border 8 lw 3 lc rgbcolor c_side
set tics format '\small $%2.1f$'

unset xtics
unset ytics
set y2tics (-4.5, -4.0, -3.5, -3.0, -2.5) tc rgbcolor c_side nomirror
set y2label "$\\Delta E^{\\text{HF}}$ (kcal/mol)" tc rgbcolor c_side offset -1.4
plot "water-water._6_311_pp_g_ss_AUX_accq.dat" u 1:2 axis x1y2  with lines       ls 5 notitle

set size ratio 0.8 s, s
set origin o11x, o11y
set border 7 lw 3 lc rgbcolor c_border
unset y2tics
unset y2label
set xtics  ("" -0.4, "" -0.2, "" 0.0, "" 0.2, "" 0.4, "" 0.6, "" 0.8) tc rgbcolor c_border
set ytics  (-3.0, -2.5, -2.0, -1.5, -1.0, -0.5, 0.0) tc rgbcolor c_border nomirror 
unset xlabel

set key at graph 0.96, 0.45 box  opaque samplen 0.1 spacing 1.7 width 5.8 height 0.7 font ",8"
set ylabel "$E^{\\text{CT}}$ (kcal/mol)" offset 2 #tc rgbcolor c_border
set label 22 "\\Large \\textbf{(a)}" front at graph graph_label_x,graph_label_y tc rgbcolor "#000000"

plot "water-water._6_311_pp_g_ss_AUX_accq.dat" \
                       u 1:13 axis x1y1  with lines       ls 4 notitle, \
     ''                u 1:10 axis x1y1  with linespoints ls 2 notitle, \
     ''                u 1: 7 axis x1y1  with linespoints ls 3 notitle, \
     ''                u 1: 9 axis x1y1  with linespoints ls 1 notitle, \
     NaN                      axis x1y1  with linespoints ls 1 t 'OEP', \
     NaN                      axis x1y1  with linespoints ls 2 t 'OL', \
     NaN                      axis x1y1  with linespoints ls 3 t 'EFP2', \
     NaN                      axis x1y1  with lines       ls 4 t 'Est'

# ------ Graph II 
unset key
unset label 22

set xrange [-0.3:1.0]
set yrange [-10:0.05]
set y2range [-20:-12]

set size ratio 0.8 s, s
set origin o21x, o21y
set border 8 lw 3 lc rgbcolor c_side
unset xtics
unset ytics
unset ylabel
set y2tics (-20, -18.0, -16.0, -14.0, -12.0) tc rgbcolor c_side nomirror 
set y2label "$\\Delta E^{\\text{HF}}$ (kcal/mol)" tc rgbcolor c_side offset -2.3
plot "water-ammonium._6_311_pp_g_ss_AUX_accq.dat" u 1:2 axis x1y2  with lines       ls 5 notitle

set size ratio 0.8 s, s
set origin o21x, o21y
set border 7 lw 3 lc rgbcolor c_border
unset y2tics
unset y2label
set xtics ("" -0.4, "" -0.2, "" 0.0, "" 0.2, "" 0.4, "" 0.6, "" 0.8) tc rgbcolor c_border
set ytics (-10.0, -8.0, -6.0, -4.0, -2.0, 0.0) tc rgbcolor c_border nomirror
set ylabel "$E^{\\text{CT}}$ (kcal/mol)" offset 2.8
set label 22 "\\Large \\textbf{(b)}" front at graph graph_label_x,graph_label_y tc rgbcolor "#000000"
plot "water-ammonium._6_311_pp_g_ss_AUX_accq.dat" \
                       u 1: 7 axis x1y1  with linespoints ls 3 notitle, \
     ''                u 1:13 axis x1y1  with lines       ls 4 notitle, \
     ''                u 1: 9 axis x1y1  with linespoints ls 1 notitle, \
     ''                u 1:10 axis x1y1  with linespoints ls 2 notitle, \
     NaN                      axis x1y1  with linespoints ls 1 t 'OEP', \
     NaN                      axis x1y1  with linespoints ls 2 t 'OL', \
     NaN                      axis x1y1  with linespoints ls 3 t 'EFP2',\
     NaN                      axis x1y1  with lines       ls 4 t 'Est'

# ------ Graph III
unset key
unset label 22

set xrange [-0.3:1.0]
set yrange [-30:0.05]
set y2range [-119:-91]

set size ratio 0.8 s, s
set origin o31x, o31y
set border 8 lw 3 lc rgbcolor c_side
unset xtics
unset ytics
unset ylabel
set y2tics (-115.0, -110.0, -105.0, -100.0, -95.0, -90.0) tc rgbcolor c_side nomirror
set y2label "$\\Delta E^{\\text{HF}}$ (kcal/mol)" tc rgbcolor c_side offset -2.4
plot "ammonium-nitrate._6_311_pp_g_ss_AUX_accq.dat" u 1:2 axis x1y2  with lines       ls 5 notitle


set size ratio 0.8 s, s
set origin o31x, o31y
set border 7 lw 3 lc rgbcolor c_border
unset y2tics
unset y2label
set xtics (-0.4, -0.2, 0.0, 0.2, 0.4, 0.6, 0.8) tc rgbcolor c_border nomirror
set ytics (-30.0, -25.0, -20.0, -15.0, -10.0, -5.0, 0.0) tc rgbcolor c_border nomirror
set ylabel "$E^{\\text{CT}}$ (kcal/mol)" offset 2.6
set xlabel "Displacement (\\AA)"
set label 22 "\\Large \\textbf{(c)}" front at graph graph_label_x,graph_label_y tc rgbcolor "#000000"
#plot "ammonium-nitrate._6_311_pp_g_ss_AUX_accq.dat" \
#                       u 1: 2 axis x1y2  with lines       lw 2 lc rgbcolor c_side notitle, \
#     ''                u 1: 7 axis x1y1  with linespoints pt 7 lc rgbcolor c_efp2 notitle, \
#     ''                u 1:13 axis x1y1  with lines       lw 2 lc rgbcolor c_est  notitle, \
#     ''                u 1: 9 axis x1y1  with linespoints pt 7 lc rgbcolor c_ol   notitle, \
#     ''                u 1:10 axis x1y1  with linespoints pt 6 lc rgbcolor c_ol   notitle, \
#     NaN                      axis x1y1  with linespoints pt 7 lc rgbcolor c_ol   t 'OEP', \
#     NaN                      axis x1y1  with linespoints pt 6 lc rgbcolor c_ol   t 'OL', \
#     NaN                      axis x1y1  with linespoints pt 7 lc rgbcolor c_efp2 t 'EFP2',\
#     NaN                      axis x1y1  with lines       lw 2 lc rgbcolor c_est t 'Est'
plot "ammonium-nitrate._6_311_pp_g_ss_AUX_accq.dat" \
                       u 1: 7 axis x1y1  with linespoints ls 3 notitle, \
     ''                u 1:13 axis x1y1  with lines       ls 4 notitle, \
     ''                u 1: 9 axis x1y1  with linespoints ls 1 notitle, \
     ''                u 1:10 axis x1y1  with linespoints ls 2 notitle, \
     NaN                      axis x1y1  with linespoints ls 1 t 'OEP', \
     NaN                      axis x1y1  with linespoints ls 2 t 'OL', \
     NaN                      axis x1y1  with linespoints ls 3 t 'EFP2',\
     NaN                      axis x1y1  with lines       ls 4 t 'Est'



# ------------------------------------------------------------------------------------------------- #


unset multiplot
