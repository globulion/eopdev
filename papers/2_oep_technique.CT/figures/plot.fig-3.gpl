#!/usr/bin/gnuplot
#
# ------------------------------------------------
# Figure 3. Charge-Transfer Energy OEP Paper.
# Bartosz BÅ‚asiak, Gundelfingen 12 November 2019
# ------------------------------------------------
#
set terminal epslatex size 16.1cm,8.1cm color colortext standalone \
   font "cmss, 8" \
   header \
   "\\newcommand{\\ft}[0]{\\footnotesize}\\usepackage{amsmath}\\usepackage{amssymb}\\usepackage{upgreek}"

c_border = "#515151"
c_side = "#c7c7c7"
c_ol   = "#0c8a1b"
c_est  = "#333333"
c_efp2 = "#0035ba"
c_oep  = c_ol

set style line 1 lc rgb "#404040" lw 0.8 dt 2
set style line 2 lc rgb c_side   lw 0.5 dt 3

psn = 1.0
set style line 11 lc rgb c_efp2 lt 1 lw 2 pt 4 pi -1 ps psn  # EFP2
set style line 12 lc rgb c_oep  lt 1 lw 2 pt 5 pi -1 ps psn  # OEP


set pointintervalbox 2.9
set size ratio -1

# global settings for each subplot

# output file
set output 'fig-3.tex'
set loadpath "./data/"
set multiplot

# physical scaling factors
c1 = 627.509             # --> kcal/mole


# origins
o11x =-0.16; o11y = 0.1;
o12x = 0.35; o12y = 0.1;

# R2 box position
ob1x = 0.07; ob1y = 0.86;
ob2x = 0.41; ob2y = 0.96;

# scale
s    = 0.5;

# margins
set bmargin 2
set lmargin 0
set rmargin 0
set tmargin 7

# label positions
graph_label_x1= -0.37
graph_label_y1=  1.10
graph_label_x2= -0.53
graph_label_y2=  1.10

set format x "%2.1f"
set format y "%2.1f"

set border lw 3 lc rgbcolor c_border


# ------ Graph I
set xrange [-5:0]
set yrange [-5:0]
#set grid xtics ytics mxtics mytics ls 1, ls 2
set grid xtics ytics ls 1, ls 2

set xtics 1
set ytics 1
set mxtics 2
set mytics 2
set size ratio -1
set origin o11x, o11y
plot  x with lines lw 3 lc rgbcolor c_border notitle

set size ratio -1
set origin o11x, o11y
set xlabel "$\\Delta E^{\\text{CT}}_{\\text{Otto-Ladik}}$ (kcal/mol)" #offset 0.4 #tc rgbcolor c_border
set ylabel "$\\Delta E^{\\text{CT}}_{\\text{OEP}}$ (kcal/mol)" #offset 0.4 #tc rgbcolor c_border
set label 22 "\\Large \\textbf{(a)}" front at graph graph_label_x1,graph_label_y1 tc rgbcolor "#000000"
set object 1 rectangle from graph ob1x,ob1y to graph ob2x,ob2y lw 1
set label 11 "$R^2=0.6221$" front at graph 0.1,0.9 tc rgbcolor "#515151"
plot "stat_db_bbi_6_31_p_g_ss.dat" u 3:4 axis x1y1 with points pt 7 ps psn lc rgbcolor "#171717" notitle



# ------ Graph II
#set xtics  ("" -0.4, "" -0.2, "" 0.0, "" 0.2, "" 0.4, "" 0.6, "" 0.8) tc rgbcolor c_border
#set ytics  (-3.0, -2.5, -2.0, -1.5, -1.0, -0.5, 0.0) tc rgbcolor c_border nomirror 
set xrange [-3.0:0.0]
set yrange [-5.0:0.0]

unset label 22
unset label 11
unset object 1
unset key
unset grid
unset xlabel
unset ylabel
unset xtics
unset ytics


set grid x, y
set xlabel "$\\Delta E^{\\text{CT}}_{\\text{Est}}$ (kcal/mol)" #offset 0.4 #tc rgbcolor c_border
set ylabel "$\\Delta E^{\\text{CT}}$ (kcal/mol)" #offset 0.4 #tc rgbcolor c_border
set label 22 "\\Large \\textbf{(b)}" front at graph graph_label_x2,graph_label_y2 tc rgbcolor "#000000"
set xtics
set ytics

set key reverse Left at graph 0.931, 0.19 box opaque spacing 1.2 samplen 0.1 width 0.9 height 0.5

set size ratio -1
set origin o12x, o12y
plot  x with lines lw 3 lc rgbcolor c_border notitle,\
     "stat_db_bbi_6_31_p_g_ss.dat" \
                       u 1:2 axis x1y1 with points ls 11 notitle,\
     ''                u 1:4 axis x1y1 with points ls 12 notitle,\
     NaN               with points ls 12 ps 1.8 title 'OEP',\
     NaN               with points ls 11 ps 1.8 pt 5 title 'EFP2'



# ------------------------------------------------------------------------------------------------- #


unset multiplot
