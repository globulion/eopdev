#!/usr/bin/gnuplot
set terminal epslatex color standalone blacktext "" 12 header "\\usepackage[T1]{fontenc}\n\\usepackage{cmbright}\n\\usepackage{fancybox}\n\\usepackage{color}\n\\renewcommand{\\familydefault}{\\sfdefault}\n\\newcommand{\\ft}[0]{\\footnotesize}\n\\usepackage{microtype}\n\\DisableLigatures[f]{encoding = *, family = * }\n\\usepackage{tikz}\n"

set output 'fig-1.tex'
set size 0.95, 1.0

set xlabel 'Deviation from equilibrium geometry [\AA]'
set xlabel offset 0, 0
set ylabel 'Charge Transfer Energy [kcal/mol]'
set ylabel offset 0, 0 
set xtics offset 0, 0
set ytics offset 0, 0 
set ytics nomirror
set grid

set xtics nomirror
set tmargin 2

set xrange [-0.55:1.0]
set yrange [-5:0.1]
#set xrange [-1000:4000]
#set xtics 0,1000,4000
set format x "%3.1f"
#set border 3 lw 1

set key font ",16"
set key spacing 1.8
set key at screen 0.87, 0.35 
set key box lw 2 lc rgb '#393939'

set border back ls 20 lw 3 lc rgb '#393939'

set style line 1 lc rgb '#0060ad' lt 1 lw 2 pt 7 pi -1 ps 1.5
set style line 2 lc rgb '#a83655' lt 1 lw 2 pt 7 pi -1 ps 1.5
set style line 3 lc rgb 'forest-green' lt 1 lw 2 pt 7 pi -1 ps 1.5

set pointintervalbox 3

set datafile missing 'N'

plot 'water_water.dat' u 1:5 w lp ls 2 title "EFP2", \
     '' u 1:3 w lp ls 3 title "Murrell et al.", \
     '' u 1:4 w lp ls 1 title "OEP"
