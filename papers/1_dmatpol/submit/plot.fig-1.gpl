#!/usr/bin/gnuplot
#
# ------------------------------------------------
# Figure 1. Density Matrix Susceptibility Paper.
# Bartosz Błasiak, Wrocław 21 July 2018
# ------------------------------------------------
#
set terminal epslatex size 6.89cm,8.48cm color colortext standalone \
   font "cmss, 8" \
   header \
   "\\newcommand{\\ft}[0]{\\footnotesize}\\usepackage{amsmath}\\usepackage{amssymb}\\usepackage{upgreek}"
# Original size: 6.5cm,8.0cm ->scaling by 1.06

# global settings for each subplot
set border lw 2 lc rgbcolor "#515151"
set key inside right bottom vertical Left noreverse noenhanced autotitle nobox spacing 1.1 box lw 2.0 lt 2 lc 0 \
        samplen 0 width -5.8 height 0.3
set grid x y

# output file
set output 'fig-1.tex'
set loadpath "./reports/"

# physical scaling factors
c1 = 627.509             # --> kcal/mole
c2 = 2.541765            # --> Debye
c3 = 2.541765*0.529177249# --> Debye-Angstrom

# margins
set bmargin 4
set lmargin -3
set rmargin -3
set tmargin 1

# label positions
graph_label_x = 0.80
graph_label_y = 0.14

set format x "%2.0f"
set format y "%2.0f"
set xrange [-11.0:3.0]
set yrange [-22.0:0.4]
set xtics 3

set size ratio -1
#set xtics (-6.0, -4.0, -2.0, 0.0)
#set ytics (-6.0, -4.0, -2.0, 0.0)
set xlabel "$\\delta E_{\\text{Full QM}}^{\\text{Pol}}$ (kcal/mol)" offset 0.0,-0.3
set ylabel "$\\delta E_{\\text{Theory}}^{\\text{Pol}}$ (kcal/mol)"

plot \
     "REPORT.POLAR.G.WATER_EFIELD_1_0_FAVE0.035_100.DAT"    u (c1*($1)):(c1*($2)) w p pt 6 ps 1.3 lc rgbcolor "#A091FF" notit, \
     "REPORT.POLAR.G.WATER_EFIELD_2_0_FAVE0.035_100.DAT"    u (c1*($1)):(c1*($2)) w p pt 6 ps 1.3 lc rgbcolor "#FF477F" notit, \
     "REPORT.POLAR.A.WATER_EFIELD_1_0_FAVE0.035_100_FF.DAT" u (c1*($1)):(c1*($2)) w p pt 7        lc rgbcolor "#2200FF" notit, \
     "REPORT.POLAR.A.WATER_EFIELD_2_0_FAVE0.035_100_FF.DAT" u (c1*($1)):(c1*($2)) w p pt 7        lc rgbcolor "#FF004D" notit, \
     "REPORT.POLAR.A.WATER_EFIELD_1_0_FAVE0.035_100.DAT"    u (c1*($1)):(c1*($2)) w p pt 5 ps 0.6 lc 0                  notit, \
     NaN w p pt 7        lc rgbcolor "#2200FF" t "\\small $R_{\\text{max}}=1$, Eq.(5)" , \
     NaN w p pt 7        lc rgbcolor "#FF004D" t "\\small $R_{\\text{max}}=2$, Eq.(5)" , \
     NaN w p pt 6 ps 1.3 lc rgbcolor "#A091FF" t "\\small $R_{\\text{max}}=1$, Eq.(28)", \
     NaN w p pt 6 ps 1.3 lc rgbcolor "#FF477F" t "\\small $R_{\\text{max}}=2$, Eq.(28)", \
     NaN w p pt 5 ps 0.6 lc 0                  t "\\small $R_{\\text{max}}=1$, Eq.(26)", \
      x w l lw 2 lc 0 notitle
